
// Gradle build file

description = 'Aerial Game Engine'

allprojects {
    group 'aerial'
    version = '0.0.3'
}

apply from: "depend.gradle"

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = 9
targetCompatibility = 9

repositories {
    mavenCentral()
}

boolean isModule(entry) {
    //TODO find a better way to find a module
    //Look at .classpath for eclipse entry kind
    return entry.kind == 'src' || entry.kind == 'lib'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    
    sourceCompatibility = 9
    targetCompatibility = 9

    sourceSets.main.java.srcDirs = ["src"]
    sourceSets.main.resources.srcDirs = ["res"]

    repositories {
        mavenCentral()
    }
    
    compileJava {
        options.fork = true
        options.incremental = true
        doFirst {
	        options.compilerArgs = [
	            '--module-path', classpath.asPath,
	        ]
        }
    }
    
    eclipse.classpath.file.whenMerged {
        entries.findAll { isModule(it) }.each { it.entryAttributes['module'] = 'true' }
    }

    // This is so that intellij can recognize resource directories defined by gradle
    idea.module {
        inheritOutputDirs = true
    }

}

dependencies {
    compile project(':aerial:aui')
    compile project(':aerial:box')
    compile project(':aerial:commons')
    compile project(':aerial:bullet')
    compile project(':aerial:editor')
    compile project(':aerial:engine')
    compile project(':aerial:graphics')
    compile project(':aerial:math')
    compile project(':aerial:standard')
    compile project(':aerial:tea')
    
    compile project(':examples')
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            
            // Remove dependencies since we already included them in jar
            pom.withXml {
                def node = it.asNode()
                node.remove(node.get("dependencies"))
            }
            
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

subprojects {

    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            mavenLocal()
        }
    }
    
}

apply from: "tasks.gradle"



// Gradle build file

description = 'Aerial Game Engine'
group 'aerial'
version = '0.0.2'

apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 9
targetCompatibility = 9

repositories {
    mavenCentral()
}

// Project level dependencies
project.ext {
    dependency_gson = dependencies.create( group: 'com.google.code.gson', name: 'gson', version: '2.8.3' )
}

boolean isModule(entry) {
    //TODO find a better way to find a module
    //Look at .classpath for eclipse entry kind
    return entry.kind == 'src' || entry.kind == 'lib'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'eclipse'
    
    sourceCompatibility = 9
    targetCompatibility = 9

    sourceSets.main.java.srcDirs = ["src"]
    sourceSets.main.resources.srcDirs = ["res"]

    repositories {
        mavenCentral()
    }
    
    compileJava {
        options.fork = true
        options.incremental = true
        doFirst {
	        options.compilerArgs = [
	            '--module-path', classpath.asPath,
	        ]
        }
    }
    
    eclipse.classpath.file.whenMerged {
        entries.findAll { isModule(it) }.each { it.entryAttributes['module'] = 'true' }
    }

}

dependencies {
    compile project(':engine')
    compile project(':aerial:editor')
    compile project(':aerial:graphics')
    compile project(':aerial:aui')
    compile project(':aerial:commons')
    compile project(':aerial:math')
    compile project(':aerial:box')
}

jar {
    from configurations.compile .collect { zipTree it }
    exclude '**/module-info.class'
    exclude '**/*.sha1'
    exclude '**/*.git'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            
            // Remove dependencies since we already included them in jar
            pom.withXml {
                def node = it.asNode()
                node.remove(node.get("dependencies"))
            }
            
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

